buildscript {
	repositories {
		gradlePluginPortal()
		mavenCentral()
		maven { url "https://maven.fabricmc.net" }
		maven { url "https://repo.sleeping.town" }
	}
	dependencies {
		classpath "agency.highlysuspect:voldeloom:${project.voldeloom_version}"
	}
}

apply plugin: "java"
apply plugin: "agency.highlysuspect.voldeloom"

java.toolchain.languageVersion = JavaLanguageVersion.of(11) // Last version able to set a --release as low as 6
compileJava.options.release.set(6) // Forge doesn't understand classes compiled to versions of the class-file format newer than Java 6's

archivesBaseName = project.archivesBaseName
version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

volde {
	runs {
		client {
			programArg "unilock"
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	forge "net.minecraftforge:forge:${project.forge_version}:universal@zip"
	mappings "net.minecraftforge:forge:${project.forge_version}:src@zip"
}

processResources {
	inputs.property "version", project.version

	filesMatching("src/main/resources/mcmod.info") {
		expand "version": project.version
	}
}

tasks.jar.dependsOn(processResources)

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
}